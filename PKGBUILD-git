# Maintainer: GaÃ«l PORTAY <gael.portay@gmail.com>

pkgname=(domake-git
	 domake-docker-make-git
	 domake-linux-platforms-git)
pkgver=master
pkgrel=1
pkgdesc='Docker make'
arch=(any)
url=https://github.com/gportay/domake
license=(LGPL-2.1-or-later)
depends=(bash)
makedepends=(asciidoctor bash-completion git)
checkdepends=(shellcheck)
source=(domake-git::git+https://github.com/gportay/domake.git)
sha256sums=(SKIP)
validpgpkeys=(8F3491E60E62695ED780AC672FA122CA0501CA71)
changelog=CHANGELOG.md

pkgver() {
	cd "$srcdir/domake-git"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/domake-git"
	make domake.1.gz
}

check() {
	cd "$srcdir/domake-git"
	make -k check
}

package_domake-git() {
	depends+=(dosh)
	optdepends+=(domake-docker-make)
	provides=(domake)
	conflicts=(domake)

	cd "$srcdir/domake-git"
	make DESTDIR="$pkgdir" PREFIX="/usr" install install-doc install-bash-completion
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/domake-git/LICENSE"
}

package_domake-docker-make-git() {
	pkgdesc='Docker CLI plugin for domake'
	depends+=(domake)
	provides=(domake-docker-make)
	conflicts=(domake-docker-make)

	cd "$srcdir/domake-git"
	make DESTDIR="$pkgdir" PREFIX="/usr" install-docker-cli-plugin
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/domake-docker-make-git/LICENSE"
}

package_domake-linux-platforms-git() {
	pkgdesc='Docker shell for linux platforms'
	depends+=(domake)
	provides=(domake-linux-platforms)
	conflicts=(domake-linux-platforms)

	cd "$srcdir/domake-git"
	make DESTDIR="$pkgdir" PREFIX="/usr" install-linux-platforms
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/domake-linux-platforms-git/LICENSE"
}

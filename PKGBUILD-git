# Maintainer: GaÃ«l PORTAY <gael.portay@gmail.com>

pkgname=(domake-git)
pkgver=master
pkgrel=1
pkgdesc='Docker make'
arch=(any)
url=https://github.com/gportay/domake
license=(LGPL-2.1-or-later)
depends=(bash)
makedepends=(asciidoctor bash-completion git)
checkdepends=(shellcheck)
source=(domake-git::git+https://github.com/gportay/domake.git)
sha256sums=(SKIP)
validpgpkeys=(8F3491E60E62695ED780AC672FA122CA0501CA71)
changelog=CHANGELOG.md

pkgver() {
	cd "$srcdir/domake-git"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/domake-git"
	make domake.1.gz
}

check() {
	cd "$srcdir/domake-git"
	make -k check
}

package_domake-git() {
	depends+=(dosh)
	provides=(domake)
	conflicts=(domake)

	cd "$srcdir/domake-git"
	make DESTDIR="$pkgdir" PREFIX="/usr" install install-doc install-bash-completion install-docker-cli-plugin install-linux-platforms
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/domake-git/LICENSE"
}

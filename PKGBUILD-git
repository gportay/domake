# Maintainer: GaÃ«l PORTAY <gael.portay@gmail.com>

pkgname=(domake-git
	 domake-docker-make-git
	 domake-linux-amd64-git
	 domake-linux-arm64-git
	 domake-linux-arm-git
	 domake-linux-ppc64le-git
	 domake-linux-riscv64-git
	 domake-linux-s390x-git)
pkgver=master
pkgrel=1
pkgdesc='Docker make'
arch=(any)
url=https://github.com/gportay/domake
license=(LGPL-2.1-or-later)
depends=(bash)
makedepends=(asciidoctor bash-completion git)
checkdepends=(shellcheck)
source=(domake-git::git+https://github.com/gportay/domake.git)
sha256sums=(SKIP)
validpgpkeys=(8F3491E60E62695ED780AC672FA122CA0501CA71)

pkgver() {
	cd "$srcdir/domake-git"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/domake-git"
	make domake.1.gz
}

check() {
	cd "$srcdir/domake-git"
	make -k check
}

package_domake-git() {
	depends+=(dosh)
	optdepends+=(domake-docker-make)
	provides=(domake)
	conflicts=(domake)

	cd "$srcdir/domake-git"
	make DESTDIR="$pkgdir" PREFIX="/usr" install install-doc install-bash-completion
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/domake-git/LICENSE"
}

package_domake-docker-make-git() {
	pkgdesc='Docker CLI plugin for domake'
	depends+=(domake)
	provides=(domake-docker-make)
	conflicts=(domake-docker-make)

	cd "$srcdir/domake-git"
	make DESTDIR="$pkgdir/" PREFIX="/usr" install-docker-cli-plugin
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/domake-docker-make-git/LICENSE"
}

package_domake-linux-amd64-git() {
	pkgdesc='Docker shell for linux/amd64 platform'
	depends+=(domake)
	provides=(domake-linux-amd64)
	conflicts=(domake-linux-amd64)

	cd "$srcdir/domake-git"
	make DESTDIR="$pkgdir" PREFIX="/usr" install-linux-amd64-domake
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/domake-linux-amd64-git/LICENSE"
}

package_domake-linux-arm64-git() {
	pkgdesc='Docker shell for linux/arm64 platform'
	depends+=(domake)
	provides=(domake-linux-arm64)
	conflicts=(domake-linux-arm64)

	cd "$srcdir/domake-git"
	make DESTDIR="$pkgdir" PREFIX="/usr" install-linux-arm64-domake
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/domake-linux-arm64-git/LICENSE"
}

package_domake-linux-arm-git() {
	pkgdesc='Docker shell for linux/arm platform'
	depends+=(domake)
	provides=(domake-linux-arm)
	conflicts=(domake-linux-arm)

	cd "$srcdir/domake-git"
	make DESTDIR="$pkgdir" PREFIX="/usr" install-linux-arm-domake install-linux-arm-v6-domake install-linux-arm-v7-domake
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/domake-linux-arm-git/LICENSE"
}

package_domake-linux-ppc64le-git() {
	pkgdesc='Docker shell for linux/ppc64le platform'
	depends+=(domake)
	provides=(domake-linux-ppc64le)
	conflicts=(domake-linux-ppc64le)

	cd "$srcdir/domake-git"
	make DESTDIR="$pkgdir" PREFIX="/usr" install-linux-ppc64le-domake
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/domake-linux-ppc64le-git/LICENSE"
}

package_domake-linux-riscv64-git() {
	pkgdesc='Docker shell for linux/riscv64 platform'
	depends+=(domake)
	provides=(domake-linux-riscv64)
	conflicts=(domake-linux-riscv64)

	cd "$srcdir/domake-git"
	make DESTDIR="$pkgdir" PREFIX="/usr" install-linux-riscv64-domake
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/domake-linux-riscv64-git/LICENSE"
}

package_domake-linux-s390x-git() {
	pkgdesc='Docker shell for linux/s390x platform'
	depends+=(domake)
	provides=(domake-linux-s390x)
	conflicts=(domake-linux-s390x)

	cd "$srcdir/domake-git"
	make DESTDIR="$pkgdir" PREFIX="/usr" install-linux-s390x-domake
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/domake-linux-s390x-git/LICENSE"
}
